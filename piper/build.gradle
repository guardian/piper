apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:24.1.1'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

group = 'com.gu.android'
version = '0.1.2'

install {
    repositories.mavenInstaller {
        pom.project {
            name 'Piper'
            description 'An Android library to reduce fiddly boilerplate SQLite code'
            url 'https://github.com/guardian/piper'
            inceptionYear '2016'

            packaging 'aar'
            groupId 'com.gu.android'
            artifactId 'piper'
            version '0.1.2'

            licenses {
                license {
                    name 'The MIT License (MIT)'
                    url 'https://opensource.org/licenses/MIT'
                }
            }
            scm {
                connection 'https://github.com/guardian/piper.git'
                url 'https://github.com/guardian/piper'

            }
            developers {
                developer {
                    id = 'maxspencer'
                    name 'Max Spencer'
                }
            }
        }
    }
}

Properties bintrayProperties = new Properties()
bintrayProperties.load(project.rootProject.file('bintray.properties').newDataInputStream())

bintray {
    user = bintrayProperties.getProperty('user')
    key = bintrayProperties.get('key')
    configurations = ['archives']
    pkg {
        repo = 'android'
        name = 'piper'
        userOrg = 'guardian'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/guardian/piper.git'
        publish = true
        version {
            name = '0.1.2'
            desc = 'Piper 0.1.2'
            released = new Date()
            vcsTag = 'v0.1.2'
        }
    }
}
